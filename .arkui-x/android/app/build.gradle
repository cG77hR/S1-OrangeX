import groovy.json.JsonSlurper

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    def moduleJsonFile = file("$rootDir/app/src/main/assets/arkui-x/entry/module.json")

    defaultConfig {
        applicationId "io.github.wly5556.s1orangeX"
        minSdkVersion 26
        targetSdkVersion 33
        if (moduleJsonFile.exists()) {
            def moduleJson = new JsonSlurper().parse(moduleJsonFile)
            versionCode (moduleJson.app?.versionCode ?: 1) as Integer
            versionName moduleJson.app?.versionName?.toString() ?: "1.0.0"
        } else {
            versionCode 1
            versionName "1.0.0"
        }

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dynamicFeatures = []

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.core:core:1.10.1'
}
